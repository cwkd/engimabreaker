global Alufn {
  const ADD   = 6b000000;
  const SUB   = 6b000001;
  const MUL   = 6b000010;
  const DIV   = 6b000011;
  const AND   = 6b011000;
  const OR    = 6b011110;
  const XOR   = 6b010110;
  const A     = 6b011010;
  const SHL   = 6b100000;
  const SHR   = 6b100001;
  const SRA   = 6b100011;
  const CMPEQ = 6b110011;
  const CMPLT = 6b110101;
  const CMPLE = 6b110111;
}

module alu8 (
    input a[8],
    input b[8],
    input alufn[6],
    output alu[8],
    output test[3],
    input cin,
    output cout
  ) {

  // submodules of the alu: arithmetic, shift, boolean, compare
  arith8 arith;
  compare8 compare;
  boole8 boole;
  shift_with_rotate shift;  
  
  sig out[4][8];
  
  always {
    // input a and b to all the submodules
    arith.a = a; arith.b = b; arith.alufn = alufn[1:0]; out[0] = arith.out;
    boole.a = a; boole.b = b; boole.alufn = alufn[3:0]; out[1] = boole.boole;
    shift.a = a; shift.b = b[2:0]; shift.alufn = alufn[3:0]; out[2] = shift.out; shift.cin = cin;
    compare.z = arith.z; compare.v = arith.v; compare.n = arith.n; compare.alufn = alufn[2:1]; out[3] = compare.cmp;
    test[2] = arith.z; test[1] = arith.v; test[0] = arith.n;
    cout = 0;
    
    // select output for alu based on alufn[5:4]
    if (alufn[5:4] == 2b00){
      alu = out[0];
    } else if (alufn[5:4] == 2b01) {
      alu = out[1];
    } else if (alufn[5:4] == 2b10) {
      alu = out[2];
      cout = shift.cout;
    } else if (alufn[5:4] == 2b11) {
      alu = out[3];
    } else {
      alu = 0;
    }
  }
}
